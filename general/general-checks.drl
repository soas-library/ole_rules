package org.kuali.ole.deliver.rules.general

import org.kuali.ole.deliver.bo.OlePatronDocument
import org.kuali.ole.deliver.util.ErrorMessage
import org.kuali.ole.deliver.bo.OleAddressBo
import org.kuali.ole.deliver.drools.DroolsExchange
import java.util.Date
import java.text.SimpleDateFormat
import org.kuali.ole.service.OleCirculationPolicyServiceImpl
import org.kuali.ole.deliver.drools.DroolsConstants
import org.kuali.ole.deliver.util.DroolsResponse


rule "Is Patron Active"
 agenda-group "general-checks"
 when
 $p : OlePatronDocument(getBorrowerTypeCode() not in ("LIB"),!isActiveIndicator())
 $d : DroolsResponse()
 then
 $d.addErrorMessage("Patron is Inactive");
 end

rule "Is Patron with General Block"
 agenda-group "general-checks"
 when
 $p : OlePatronDocument(getBorrowerTypeCode() not in ("LIB"),isGeneralBlock())
 $d : DroolsResponse()
 then
 $d.addErrorMessage("Patron is blocked");
 $d.addOverridePermissions(DroolsConstants.GENERAL_BLOCK_PERMISSION);
 end

rule "Is Patron's address verified"
 agenda-group "general-checks"
 when
 $p : OlePatronDocument(getBorrowerTypeCode() not in ("LIB"),!isAddressVerified())
 $d : DroolsResponse()
 then
 $d.addErrorMessage("Address is not verified");
 end

rule "Is Patron Expired"
 agenda-group "general-checks"
 when
 $p : OlePatronDocument(getBorrowerTypeCode() not in ("LIB"),getExpirationDate() != null, $dt: getExpirationDate(), $dt.before(today()))
 $d : DroolsResponse()
 then
 $d.addErrorMessage("Patron has expired");
 $d.addOverridePermissions("Patron record expired");
 end

rule "Activation Date for Patron"
   agenda-group "general-checks"
   when
   $p : OlePatronDocument(getBorrowerTypeCode() not in ("LIB"),getActivationDate()!= null, $dt: getActivationDate(), $dt.after(today()))
   $d : DroolsResponse()
   then
   $d.addErrorMessage("Patron has an invalid activation date");
    end


rule "Check All Charges"
  agenda-group "general-checks"
  when
  $p: OlePatronDocument(getBorrowerTypeCode() not in ("LIB"),getAllCharges("OVR_DUE","REPL_FEE","SER_FEE") > 10 )
  $d : DroolsResponse()
  then
  $d.addErrorMessage("Patron's overall charges exceed 10 GBP");
  $d.addOverridePermissions(DroolsConstants.MAX_CHARGES_PERMISSION);
  end

rule "Overdue Count"
  agenda-group "general-checks"
  when
  $p: OlePatronDocument(getBorrowerTypeCode() not in ("LIB"),getTotalOverdueLoanedItemsCount() > 20 )
  $d : DroolsResponse()
  then
  $d.addErrorMessage("Patron has more than 20 items overdue");
  $d.addOverridePermissions(DroolsConstants.MAX_CHARGES_PERMISSION);
  end

rule "Patron Has Atleast One Item which is over the allow due limit"
 agenda-group "general-checks"
 when
 $p: OlePatronDocument(getBorrowerTypeCode() not in ("LIB"),isOverDueDays(3000))
 $d : DroolsResponse()
 then
 $d.addErrorMessage("Patron Has Atleast One Item which is over the allowed due limit");
 //$p.addErrorsAndPermission("Patron has max amount of replacement fees");
 end

rule "Patron Has Atleast One Item which has been recalled and has exceeded the overdue limit"
 agenda-group "general-checks"
 when
 $p: OlePatronDocument(getBorrowerTypeCode() not in ("LIB"),isRecalledAndOverDue(0))
 $d : DroolsResponse()
 then
 $d.addErrorMessage("Patron Has Atleast One Item which has been recalled and is overdue");
 //$p.addErrorsAndPermission("Patron has max amount of replacement fees");
 end

 function Date today(){
 Date date = new Date(System.currentTimeMillis());
 return date;
 }
